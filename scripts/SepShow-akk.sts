# script for September short planetarium show -- Akkana Peck
clear

# set normal defaults
zoom auto initial duration 1
deselect
flag atmosphere on
flag landscape off
flag star_twinkle off
flag show_tui_datetime on
flag cardinal_points on
flag constellation_pick on
flag planet names off
set max_mag_star_name 2
set max_mag_nebula_name 9
set sky_culture western
timerate rate 1

# auto_move_duration seems to be how long it takes nightshade
# to move to center an object while zooming it.
# If it's 0, the view will jump instantly to center the object
# before zooming.
# If it's set to the same as or longer than the zoom duration,
# then nightshade will zoom to a random patch of sky
# and then later will pan, still zoomed way in, over to the object.
# So be sure this is always set to substantially LESS than the zoom time.
# Be sure to set it! The default value may be okay on any given computer
# but then may turn out to be much longer in the planetarium.
set auto_move_duration 1

###################################################
# The upcoming lunar eclipse.
# Enters umbra:  7:07
# Maximum:       8:47
# Leaves umbra: 10:27

set moon_scale 5
flag moon_scaled on
date local 2015-09-27T18:48:00    # moonrise, already in penumbra
timerate rate 0

# Wait to start the show:
script action pause

# Run through the eclipse:
timerate rate 240

wait duration 60

script action pause

###################################################
# Start the show for September 11, the show date.
# Bandelier telescope viewing starts at 9,
# and it's not very dark before then.

timerate rate 1
date local 2015-09-11T21:00
flag moon_scaled off
set moon_scale 1
wait duration 3

# pause here, talk about planets and stars
flag planet_names on
# Saturn, Uranus, Neptune, Pluto and Ceres

# Now, you may notice that the planets are all in a line:
script action pause

flag ecliptic_line on

# That's called the ecliptic.
# PAUSE to talk about planets, the ecliptic and Saturn:
script action pause

flag ecliptic_line off
wait duration 1

###################################################
# Saturn.

# Talk about Saturn
select planet Saturn
flag track_object on
wait duration 5

# Zoom in to Saturn to talk about rings and moons.
zoom fov 0.15 duration 3
timerate rate 1600
wait duration 15

# PAUSE to talk about Saturn:
script action pause

# Back to the whole-sky view
timerate rate 1
deselect

# Set the date back, since we advanced it a lot while watching Saturn's moons.
date local 2015-09-11T21:00

zoom auto initial duration 3
wait duration 5

flag planet_names off

#
# Now let's show some constellations and deep-sky objects.
#

###################################################
# Good stuff in the core of the Milky Way.
# Start here because we were already showing Sgr for Saturn.

select constellation Sco
flag constellation_drawing on
select
select constellation Sgr

# PAUSE to talk:
script action pause

deselect
flag constellation_drawing off

# With constellation lines off, talk about scanning the area
# in binoculars or just the naked eye, and Antares.
script action pause

# Lagoon and Trifid:
select nebula M8
wait duration 2

# Lagoon and Trifid from Ljubinko Jovanovic via Wikimedia Commons:
image action load name lagoon-trifid filename images/1024px-The_Trifid_Nebula_and_The_Lagoon_Nebula-border.jpg alpha 1 coordinate_system dome scale 40 altitude 60 azimuth 215

# PAUSE to talk:
script action pause

# Open clusters M6 and M7:
select nebula M7
wait duration 2

# M6 and M7, screenshot (and contrast enhanced) from nightshade itself:
image action load name m6-m7 filename images/m6-m7.png alpha 1 scale 75 coordinate_system dome scale 30 altitude 30 azimuth 240

# PAUSE to talk:
script action pause

# M22:
select nebula M22
wait duration 2

# M22, screenshot from nightshade:
image action load name m22 filename images/m22.png alpha 1 coordinate_system dome scale 30 altitude 40 azimuth 155

# PAUSE to talk:
script action pause

# Fade out all three. This doesn't work.
# image name lagoon-trifid alpha 0 duration 3
# image name m6-m7 alpha 0 duration 3
# image name m22 alpha 0 duration 3

image name lagoon-trifid action drop
image name m6-m7 action drop
image name m22 action drop

wait duration 2

###################################################
# Big Dipper, Polaris, Cassiopeia, Mizar and M51.

# Show the Big Dipper, for orientation purposes.
select constellation UMa
flag constellation_drawing on

# PAUSE to talk:
script action pause

select

# Select Polaris
select hp 11767

# PAUSE to talk about Polaris:
script action pause

select

select constellation Cas
wait duration 5

flag constellation_drawing off

# Talk about Cassiopeia and cicumpolar constellations:
timerate rate 1500
flag object_trails on
wait duration 20
timerate rate 1
script action pause

# Back to original time.
date local 2015-09-11T21:00
deselect
# zoom auto initial

# Select Mizar
select constellation UMa
flag constellation_drawing on
select hp 65378
flag track_object on
wait duration 7
flag constellation_drawing off
wait duration 3
select pointer off
zoom fov 20 duration 3
wait duration 4

# PAUSE to talk about Alcor and Mizar:
script action pause

# Switch selection to Alcor so the bullseye thingie won't interfere
# with seeing Mizar as a double:
# select hp 65477
# flag track_object on

# Zoom in to show that Mizar is itself a double:
zoom fov .5 duration 3

# PAUSE to talk about double stars in general:
script action pause

zoom auto initial duration 3
deselect
wait duration 5

# Whirlpool galaxy:
select nebula m51
wait duration 2
image action load name m51 filename images/711px-M51_Hubble_Remix.jpg alpha 1 coordinate_system dome scale 35 altitude 25 azimuth 290

# PAUSE to talk:
script action pause

# M13
select constellation Her
flag constellation_drawing on
wait duration 5
flag constellation_drawing off
select nebula M13
wait duration 2
# M13 from Portscan via Wikimedia Commons:
# https://commons.wikimedia.org/wiki/File:Cumulo-globular-m13.JPG
image action load name m13 filename images/1024px-Cumulo-globular-m13.JPG alpha 1 coordinate_system dome scale 35 altitude 75 azimuth 200

# PAUSE to talk:
script action pause

deselect
image name m51 action drop
image name m13 action drop
wait duration 3

###################################################
# The Summer Triangle

select constellation Cyg
flag constellation_drawing on

# PAUSE to talk
script action pause

# Select Albireo:
select hp 95947
flag track_object on
wait duration 5
zoom fov 1.2 duration 3
select pointer off

# PAUSE to talk
script action pause

zoom auto initial duration 3
wait duration 5

# Dumbell nebula:
flag constellation_drawing off
select nebula M27
wait duration 2

# Dumbell nebula, from Fryns on Wikimedia Commons:
# https://commons.wikimedia.org/wiki/File:M27_Zoom.jpg
image action load name m27 filename images/1024px-M27_Zoom.jpg alpha 1 coordinate_system dome scale 30 altitude 45 azimuth 105

# PAUSE to talk:
script action pause

# The Ring nebula:
select constellation Lyr
flag constellation_drawing on
wait duration 5
flag constellation_drawing off
select nebula M57
wait duration 2
image action load name m57 filename images/Hubble_ring_m57.jpg alpha 1 coordinate_system dome scale 15 altitude 75 azimuth 270

# PAUSE to talk:
script action pause

# Show Aquila and M11 just to finish out the Summer Triangle:
select constellation Aql
flag constellation_drawing on
wait duration 5
flag constellation_drawing off
select nebula M11
wait duration 2

# M11 from Rawastrodata via Wikimedia Commons:
# https://commons.wikimedia.org/wiki/File:The_Wild_Duck_Cluster_M11.jpg
image action load name m11 filename images/The_Wild_Duck_Cluster_M11.jpg alpha 1 coordinate_system dome scale 25 altitude 27 azimuth 200

# PAUSE to talk:
script action pause

# Now drop those three:
image name m57 action drop
image name m27 action drop
image name m11 action drop

# Andromeda
select constellation Peg
select
select constellation And
flag constellation_drawing on

# PAUSE to talk about the Great Square of Pegasus:
script action pause

flag constellation_drawing off
select nebula M31
wait duration 2

# M31 from Adam Evans via Wikimedia Commons:
# https://commons.wikimedia.org/wiki/File:Andromeda_Galaxy_%28with_h-alpha%29.jpg
image action load name m31 filename images/Andromeda_Galaxy_h-alpha.jpg alpha 1 coordinate_system dome scale 45 altitude 55 azimuth 80

# PAUSE to talk about M31 -- and point out the Milky Way,
# a very similar galaxy!
script action pause

###############################################################
# Finally, turn everything off.
flag constellation_drawing off
deselect

# Leaving maybe the coolest object left: the Milky Way!
# PAUSE to talk about it:
# script action pause

# Notes for specific objects:

# Lunar eclipses

# Saturn

# Pluto, Ceres

# M8, M20

# M6 is thought to be about 1,600 light-years away,
# while M7 is about half that far, at 800 light-years.

# M22
# About 12 billion years old. About 70,000 stars?
# One of the nearest globulars at 10,600 light-years away.
# (Makes it useful for studying individual stars.)
# Two black holes of between 10 and 20 solar masses each.

# M51

# M13
# Visible with the naked eye on a really clear night; easy in binoc.
# Several hundred thousand stars.
# 11.65 billion years old.
# 25,100 light-years away.
# About 145 light-years in diameter.

# M27
# M57

# M11, "wild duck cluster".
# About 2900 stars.
# 220 million years old.

# M31
# 1 trillion stars
# 2.5 million light-years away
# May collide with us in 3-4 billion years.

# Milky Way
# 200-400 billion in the Milky Way
# Oldest known object in the halo, 13.8 billion years
#    (about the same as the age of the universe!)
# M4 is about 12.7 billion years.
# The galactic disk is more like 8.8 billion years.

