# script for March 4 planetarium show -- Akkana Peck
# with eclipse sequence from Galen Gisler
clear

# set normal defaults
flag atmosphere off
flag landscape off
flag star_twinkle off
flag show_tui_datetime on
flag cardinal_points on
flag constellation_pick on
flag ecliptic_line off
flag object_trails off
flag planet_names off
flag nebulae off
flag nebula_names off
set max_mag_star_name 1.7
set max_mag_nebula_name 7
set auto_move_duration 10
set sky_culture western
set star_limiting_mag 7
set zoom_offset -0.5

flag moon_scaled off

# set to evening of March 4th
date local 2016-03-04T22:00:00
timerate rate 1
wait duration 2
# script action pause
######################################

# select the visible zodiac constellations
select constellation Tau
flag constellation_drawing on
wait duration 1
select constellation Gem
wait duration 1
select constellation Cnc
wait duration 1
select constellation Leo
wait duration 1
select constellation Vir
wait duration 1
select constellation Ari
script action pause

flag ecliptic_line on
script action pause

flag equator_line on
script action pause
flag constellation_names on

# select the rest of the zodiac constellations 
select constellation Lib
select constellation Sco
select constellation Sgr
select constellation Aqr
select constellation Cap
select constellation Psc
select constellation Ari
script action pause

# rotate the sky to noon the next day to bring the sun into view
timerate rate 3600
wait duration 15
timerate rate 0
script action pause

flag constellation_drawing off
flag constellation_names off
select planet Sun pointer off
# flag cardinal_points off
wait duration 10
flag track_object on

# show the movement of the Sun towards and past the equator for the equinox
timerate rate 86400
wait duration 30
timerate rate 0

deselect
wait duration 10
flag cardinal_points on
flag ecliptic_line off
flag equator_line off

######################################
# March 2016 eclipse sequence
flag moon_scaled off

# show map of where we're going
image action load name map filename "images/March eclipse map.png" coordinate_system dome alpha 1 scale 50 altitude 30 azimuth 180

# set to March 9th, 90 minutes before totality
date utc 2016-03-09T00:10:00
wait duration 5

# move to coordinates of Woleai
moveto latitude 7.3732 longitude 143.9051 duration 10
wait duration 20

# drop the map
image name map action drop

flag ecliptic_line on
flag planet_names on

select planet Sun pointer off
wait duration 10
flag track_object on
wait duration 10
zoom fov 7 duration 5 duration 5
flag ecliptic_line off
flag planet_names off
timerate rate 60  # 60 seconds per second
wait duration 60
timerate rate 0
flag atmosphere on
timerate rate 60  # 60 seconds per second
wait duration 32
timerate rate 0
flag atmosphere off
wait duration 30
flag atmosphere on
timerate rate 60  # 60 seconds per second
wait duration 30
timerate rate 0
flag atmosphere off
timerate rate 60  # 60 seconds per second
wait duration 60
timerate rate 0
wait duration 5

deselect

zoom auto out
wait duration 10

moveto latitude 35.55 longitude -106.27 duration 10

# end March 2016 eclipse sequence

########## Orion nebula ###############################
select constellation Ori
flag constellation_drawing on
script action pause

flag constellation_drawing off
select nebula M42/M43
script action pause

# Rawastrodata on Wikimedia Commons
# https://commons.wikimedia.org/wiki/File:The_Orion_Nebula_M42.jpg
image action load name m42 filename "images//The_Orion_Nebula_M42.jpg" coordinate_system dome alpha 1 scale 50 altitude 28 azimuth 200
script action pause

# Rogelio Bernal Andreo on Wikimedia Commons
# https://en.wikipedia.org/wiki/Orion's_Belt#/media/File:Orion_Head_to_Toe.jpg
image action load name orion filename "images//Orion_Head_to_Toe.jpg" coordinate_system dome alpha 1 scale 50 altitude 70 azimuth 185
script action pause

deselect
image name orion action drop
image name m42 action drop

########## Spring galaxies ###############################
# Show how galaxies are clustered away from the Milky Way

# Make the Milky Way especially obvious:
set milky_way_intensity 3

timerate rate 600
wait duration 5
timerate rate 1
script action pause

flag nebulae on
flag nebula_names on
set max_mag_nebula_name 11
script action pause

flag nebula_names off
set max_mag_nebula_name 9

flag constellation_drawing on

# How to find Virgo:
select constellation UMa
flag constellation_drawing on
script action pause

select constellation Boo
script action pause

select constellation Vir
script action pause

# Virgo galaxies:

flag constellation_drawing off
deselect

# Show the Virgo cluster
# ESO
# https://en.wikipedia.org/wiki/File:ESO-M87.jpg
image action load name virgo filename "images//ESO-M87.jpg" coordinate_system dome alpha 1 scale 50 altitude 50 azimuth 110
script action pause

# Joseph D. Schulman on Wikimedia Commons:
# https://en.wikipedia.org/wiki/Messier_91#/media/File:Messier_91_%28M91%29.jpg
image action load name M91 filename "images/Messier_91.jpg" coordinate_system dome alpha 1 scale 50 altitude 40 azimuth 240

# Adam Block/Mount Lemmon SkyCenter/University of Arizona
# https://commons.wikimedia.org/wiki/File:M99s.jpg
image action load name M99 filename "images/M99s.jpg" coordinate_system dome alpha 1 scale 50 altitude 35 azimuth 85

# ESO by way of Wikimedia Commons
# https://en.wikipedia.org/wiki/Messier_100#/media/File:Messier_100_%E2%80%94_Grand_Design_Splendour.jpg
image action load name M100 filename "images/Messier_100_Grand_Design_Splendour.jpg" coordinate_system dome alpha 1 scale 50 altitude 60 azimuth 300

# Astrofotograf√≠a via Wikimedia Commons
# https://commons.wikimedia.org/wiki/File:M-98.jpg
image action load name M98 filename "images/M-98.jpg" coordinate_system dome alpha 1 scale 50 altitude 50 azimuth 50 rotation 65

# HST via Wikimedia Commons
# https://upload.wikimedia.org/wikipedia/commons/0/07/Messier_87_Hubble_WikiSky.jpg
image action load name M87 filename "images/Messier_87_Hubble_WikiSky.jpg" coordinate_system dome alpha 1 scale 50 altitude 30 azimuth 170

script action pause
image name virgo action drop
image name M87 action drop
image name M91 action drop
image name M100 action drop
image name M99 action drop
image name M98 action drop

select constellation Leo
flag constellation_drawing on
script action pause

flag constellation_drawing off
select nebula M65
wait duration 4

# Hewholooks on Wikimedia Commons
# https://commons.wikimedia.org/wiki/File:LeoTripletHunterWilson.jpg
image action load name leo_trio filename "images/LeoTripletHunterWilson.jpg" coordinate_system dome alpha 1 scale 50 altitude 40 azimuth 200
script action pause

image name leo_trio action drop
deselect

select constellation Uma
flag constellation_drawing on
wait duration 5
flag constellation_drawing off
select nebula M81
script action pause

image action load name m81-m82 filename "images/m81m82_zmaritsch.jpg" coordinate_system dome alpha 1 scale 50 altitude 60 azimuth 180
script action pause

image name m81-m82 action drop
deselect
set milky_way_intensity 3


date local 2016-03-04T20:00:00
zoom auto initial
timerate rate 1
wait duration 5
set zoom_offset -0.3

########## Jupiter ###############################
# Show Jupiter's moons and a Galilean moon/shadow transit.

flag cardinal_points off
flag planet_names on
select planet Jupiter
wait duration 4
select planet Jupiter pointer off
flag track_object on
wait duration 5

# Zoom in to show the dance of Jupiter's moons
flag planet_names off
zoom fov .10 duration 10
wait duration 10
# This isn't actually a wait -- it just gets us to the end
# of the 5-second zoom we just did.
flag planet_names on
date local 2016-03-21T09:30:00
timerate rate 2500

wait duration 12
flag planet_names off
script action pause
# Point out the GRS

wait duration 8
# Point out Io and Europa's shadows

script action pause

date local 2016-03-04T20:00:00
timerate rate 1

flag track_object off
zoom auto initial duration 10
wait duration 10

# Turn the pointer back on since we're returning control to the operator
# (maybe not necessary?)
select pointer on
flag cardinal_points on
