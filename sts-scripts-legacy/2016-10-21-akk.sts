# script for October planetarium show -- Akkana Peck
clear

deselect
flag track_object off
date local 2016-10-21T19:00
flag atmosphere on
set heading 0 duration 1
zoom auto initial duration 1
flag landscape on
flag show_tui_datetime on
flag cardinal_points on
flag constellation_pick on
flag constellation_drawing off
flag ecliptic_line off
flag object_trails off
flag planet_names off
flag nebulae off
flag nebula_names off
set max_mag_star_name 1.7
set max_mag_nebula_name 7
# set auto_move_duration 10
set sky_culture western
set star_limiting_mag 7
set zoom_offset -0.5
timerate rate 1
flag star_twinkle on
flag planet_names on
wait duration 5

###################################################
# Beginning of show

flag constellation_drawing off
flag constellation_names off

# Venus
select planet Venus
script action pause
flag constellation_drawing on
flag constellation_names on
script action pause

# Saturn
deselect
select constellation Oph
select planet Saturn
script action pause

# Zoom in
flag constellation_names off
flag track_object on
flag atmosphere off
flag star_twinkle off
wait duration 5
zoom fov .1 duration 10
wait duration 15
script action pause

# Show Mars
deselect
select constellation Sgr
zoom auto initial duration 10
wait duration 10
select planet Mars
wait duration 5
script action pause

# Zoom in
flag track_object on
wait duration 5
zoom fov .01 duration 10
wait duration 15
script action pause

deselect
set heading 0 duration 0
zoom auto initial duration 0
flag planet_names on
wait duration 5

# Uranus and Neptune
# select planet Uranus
timerate rate 2000
wait duration 6
timerate rate 1
flag constellation_names on
script action pause

flag planet_names off
flag constellation_names off
flag constellation_drawing off

# Go back to the current time
timerate rate -2000
wait duration 5
timerate rate 1

###################################################
# Now let's show some constellations and deep-sky objects.
###################################################
# Good stuff in the core of the Milky Way.
# Start here because we were already showing Sgr for Saturn.

select
sky_culture action load path asterisms
select constellation Tpt
flag constellation_drawing on

# Talk about Sgr and the Milky Way, and scanning the area
# in binoculars or just the naked eye, and Antares.
script action pause
flag constellation_drawing off

# Lagoon and Trifid:
select nebula M8
wait duration 2

# Lagoon and Trifid from Ljubinko Jovanovic via Wikimedia Commons:
image action load name lagoon-trifid filename images/1024px-The_Trifid_Nebula_and_The_Lagoon_Nebula-border.jpg alpha 1 coordinate_system dome scale 40 altitude 60 azimuth 215

# PAUSE to talk:
script action pause

# Open clusters M6 and M7:
select nebula M7
wait duration 2

# M6 and M7, screenshot (and contrast enhanced) from nightshade itself:
image action load name m6-m7 filename images/m6-m7.png alpha 1 scale 75 coordinate_system dome scale 30 altitude 30 azimuth 240

# PAUSE to talk:
script action pause

# M22:
select nebula M22
wait duration 2

# M22, screenshot from nightshade:
image action load name m22 filename images/m22.png alpha 1 coordinate_system dome scale 30 altitude 40 azimuth 155

# PAUSE to talk:
script action pause

# Fade out all three. This doesn't work.
# image name lagoon-trifid alpha 0 duration 3
# image name m6-m7 alpha 0 duration 3
# image name m22 alpha 0 duration 3

deselect
image name lagoon-trifid action drop
image name m6-m7 action drop
image name m22 action drop
deselect
flag constellation_drawing off

wait duration 2

###################################################
# Big Dipper, Polaris, Cassiopeia, Mizar and M51.
set heading 180 duration 4
wait duration 4

# Show the Big Dipper, for orientation purposes.
select constellation BDi
flag constellation_drawing on

# PAUSE to talk:
script action pause

select constellation PtS
wait duration 3

# Select Polaris
select hp 11767

# PAUSE to talk about Polaris:
script action pause

deselect

select constellation BDi
select
select constellation Cas
wait duration 5

# Talk about Cassiopeia and cicumpolar constellations:
# XXX This goes for half a day. Would be nice to make a full circuit.
timerate rate 2880
# This doesn't work, but supposedly will be possible with Nightshade NG:
# flag object_trails on
wait duration 30
timerate rate 1
script action pause

# Back to original time.
date local 2016-10-21T19:30
deselect

zoom auto initial duration 3
select constellation BDi
wait duration 3

# Select Mizar
select hp 65378
wait duration 5
flag constellation_drawing off
wait duration 3

# zoom fov 20 duration 3

image action load name mizar-alcor filename images/mizar-alcor.jpg alpha 1 coordinate_system dome scale 20 altitude 37 azimuth 320 rotation 35

# PAUSE to talk about Alcor and Mizar:
script action pause

# Show that Mizar is itself a double:
image action load name mizar2 filename images/mizar2.jpg alpha 1 coordinate_system dome scale 30 altitude 60 azimuth 315 rotation 35

# PAUSE to talk about double stars in general:
script action pause

image name mizar-alcor action drop
image name mizar2 action drop

deselect
wait duration 1

# Whirlpool galaxy:
select nebula m51
wait duration 2
image action load name m51 filename images/711px-M51_Hubble_Remix.jpg alpha 1 coordinate_system dome scale 35 altitude 25 azimuth 290

# PAUSE to talk:
script action pause

deselect

# M13
select constellation Her
flag constellation_drawing on
wait duration 5
flag constellation_drawing off
select nebula M13
wait duration 2
# M13 from Portscan via Wikimedia Commons:
# https://commons.wikimedia.org/wiki/File:Cumulo-globular-m13.JPG
image action load name m13 filename images/1024px-Cumulo-globular-m13.JPG alpha 1 coordinate_system dome scale 35 altitude 65 azimuth 345

# PAUSE to talk:
script action pause
flag constellation_drawing off

# The Double Cluster from Rawastrodata via Wikimedia Commons:
# https://commons.wikimedia.org/wiki/Category:Double_Cluster#/media/File:The_Double_Cluster.jpg

select nebula "Chi & h Per"
select constellation Cas
flag constellation_drawing on
wait duration 5
image action load name doublecl filename images/The_Double_Cluster.jpg alpha 1 coordinate_system dome scale 35 altitude 30 azimuth 8
script action pause
flag constellation_drawing off

deselect
flag constellation_drawing off
image name m51 action drop
image name m13 action drop
image name doublecl action drop

# set heading 0 duration 3
# wait duration 3

###################################################
# The Summer Triangle

select constellation STr
flag constellation_drawing on
script action pause
deselect

# Northern Cross:
select constellation NCr
flag constellation_drawing on

# PAUSE to talk
script action pause

# Select Albireo:
select hp 95947
wait duration 5
# Albireo, from Jefffisher10 on Wikimedia Commons:
# https://en.wikipedia.org/wiki/Albireo#/media/File:Albireo_20150919.jpg
image action load name albireo filename images/Albireo_20150919.jpg alpha 1 coordinate_system dome scale 30 altitude 50 azimuth 250

# PAUSE to talk
script action pause
image name albireo action drop

# Dumbell nebula:
flag constellation_drawing off
select nebula M27
wait duration 2

# Dumbell nebula, from Fryns on Wikimedia Commons:
# https://commons.wikimedia.org/wiki/File:M27_Zoom.jpg
image action load name m27 filename images/1024px-M27_Zoom.jpg alpha 1 coordinate_system dome scale 30 altitude 45 azimuth 75

# PAUSE to talk:
script action pause

# The Ring nebula:
select constellation Lyr
flag constellation_drawing on
wait duration 5
flag constellation_drawing off
select nebula M57
wait duration 2
image action load name m57 filename images/Hubble_ring_m57.jpg alpha 1 coordinate_system dome scale 15 altitude 65 azimuth 5

# PAUSE to talk:
script action pause

# Show Aquila and M11 just to finish out the Summer Triangle:
select constellation Aql
flag constellation_drawing on
wait duration 5
flag constellation_drawing off
select nebula M11
wait duration 2

# M11 from Rawastrodata via Wikimedia Commons:
# https://commons.wikimedia.org/wiki/File:The_Wild_Duck_Cluster_M11.jpg
image action load name m11 filename images/The_Wild_Duck_Cluster_M11.jpg alpha 1 coordinate_system dome scale 25 altitude 55 azimuth 260

# PAUSE to talk:
script action pause

# Now drop those three:
image name m57 action drop
image name m27 action drop
image name m11 action drop

deselect
select constellation STr
flag constellation_drawing on
script action pause

deselect

# Andromeda
select constellation GSq
# PAUSE to talk about the Great Square of Pegasus:
script action pause

select constellation Peg
select
select constellation And

# Lead people's eyes to M31:
script action pause

flag constellation_drawing off
select nebula M31
wait duration 2

# M31 from Adam Evans via Wikimedia Commons:
# https://commons.wikimedia.org/wiki/File:Andromeda_Galaxy_%28with_h-alpha%29.jpg
image action load name m31 filename images/Andromeda_Galaxy_h-alpha.jpg alpha 1 coordinate_system dome scale 45 altitude 50 azimuth 10

# PAUSE to talk about M31
script action pause

# Leaving maybe the coolest object left, which is rather
# similar to M31: the Milky Way.
set milky_way_intensity 4
flag constellation_drawing off
deselect

# Move back to the normal heading, keeping our intense Milky Way
set heading 0 duration 5
wait duration 7
script action pause
image name m31 action drop

# Move to the morning to talk about Jupiter and the moon.
# The time is probably around 19:40, and we want to bring it to
# tomorrow morning, say, 6am, or about 10.5 hours

# The time is probably around 19:40 now, and we want to bring it to
# tomorrow morning, say, 6am, or about 10.5 hours
flag atmosphere on
set milky_way_intensity 1
set moon_scale 5
flag moon_scaled on
timerate rate 3000
wait duration 15
timerate rate 1
wait duration 5
flag planet_names on
script action pause

########## Jupiter ###############################

# Io:            Sun Apr 03 2016 08:11:33 GMT-0600 (MDT)
# Io and Europa: Tue Apr 05 2016 02:51:33 GMT-0600 (MDT)
# Io, with Ganymede following: Wed Apr 06 2016 21:41:33 GMT-0600 (MDT)
# Io, Europa, and Io's shadow: Fri Apr 22 2016 20:11:33 GMT-0600 (MDT)

# Show Jupiter's moons and a Galilean moon/shadow transit.
set zoom_offset -0.3

select planet Jupiter
script action pause

select planet Jupiter pointer off

# Zoom in to show the dance of Jupiter's moons
flag track_object on
flag landscape off
flag atmosphere off
wait duration 3
zoom fov .25 duration 10
wait duration 10
flag planet_names on
timerate rate 10000
wait duration 25
script action pause

timerate rate 1
zoom fov .1 duration 5
date local 2016-10-31T18:00:00
wait duration 5
# script action pause
timerate rate 3000
wait duration 6
script action pause
flag planet_names off
wait duration 10

# Back to current time, zoom out.
timerate rate 1
flag track_object off
date local 2016-10-21T06:00
flag landscape on
zoom auto initial duration 0
flag atmosphere on
wait duration 3

# Moon

# Turns out you can't zoom into the moon -- Nightshade just zoomes
# somewhere else.
# select object Moon
# flag track_object on
# wait duration 10
# flag atmosphere off
# zoom fov .1 duration 1
# wait duration 10

image action load name moon filename images/moon/Phase-260.jpg alpha 1 coordinate_system dome scale 45 altitude 40 azimuth 180
script action pause
image name moon action drop

# Turn the pointer back on since we're returning control to the operator
select pointer on
