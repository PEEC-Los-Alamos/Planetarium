# script for March 4 planetarium show -- Akkana Peck
# with eclipse sequence from Galen Gisler
clear

# set normal defaults
flag atmosphere off
flag landscape off
flag star_twinkle off
flag show_tui_datetime on
flag cardinal_points on
flag constellation_pick on
flag ecliptic_line off
flag object_trails off
flag planet_names off
flag nebulae off
flag nebula_names off
set max_mag_star_name 1.7
set max_mag_nebula_name 7
set auto_move_duration 10
set sky_culture western
set star_limiting_mag 7
set zoom_offset -0.5
set milky_way_intensity 1
# Los Alamos -- since it might get reset if the script has been partially run
# moveto latitude 35.55 longitude -106.27 duration 10

flag moon_scaled off

timerate rate 1
# script action pause

######################################
# March 2016 eclipse sequence
flag moon_scaled off

# show map of where we're going
image action load name map filename "images/March eclipse map.png" coordinate_system dome alpha 1 scale 70 altitude 30 azimuth 180

# set to March 9th, 90 minutes before totality
# move to coordinates of Woleai
date utc 2016-03-09T00:10:00
moveto latitude 7.3732 longitude 143.9051 duration 10
wait duration 20

# drop the map
image name map action drop

select planet Sun pointer off
flag track_object on
wait duration 5
zoom fov 7 duration 5
timerate rate 150
wait duration 17
flag atmosphere on
wait duration 20

script action pause
# Luc Viatour on Wikimedia Commons
# https://commons.wikimedia.org/wiki/File:Solar_eclipse_1999_4.jpg
image action load name totality filename "images/Solar_eclipse_1999_4.jpg" coordinate_system dome alpha 1 scale 70 altitude 90 azimuth 0

wait duration 16
# At this point, local time is 3/8/2016 7:32:47 pm.
# But event at PEEC will go until 8:30, so speed up the last hour even more.
timerate rate 300
wait duration 10

script action pause
# End of eclipse segment: local time 2016-03-08T08:32:49
# zoom fov 7, flag atmosphere on and we're tracking the sun.

image name totality action drop
# flag atmosphere off
# deselect

# zoom auto out duration 8
# wait duration 10

# end March 2016 eclipse sequence

############# Vernal Equinox #################################

# Set conditions to match the end of the eclipse show:
# moveto latitude 7.3732 longitude 143.9051 duration 10
# date local 2016-03-08T20:32:49
# timerate rate 1
# flag atmosphere on
# flag track_object on
# select planet Sun pointer off
# flag track_object on
# zoom fov 7 duration 5
# wait duration 8
# script action pause

# Zoom out. This will also dim the atmosphere semi-gradually.
flag ecliptic_line on
flag cardinal_points off
zoom fov 180 duration 10
wait duration 10

# Show the intersection of the ecliptic and equator
flag equator_line on
flag atmosphere off
script action pause

# Move back to Los Alamos
moveto latitude 35.55 longitude -106.27 duration 10
zoom auto initial duration 10
wait duration 10
flag cardinal_points on

# Set the date to tonight.
date local 2016-03-04T20:00:00

flag ecliptic_line off
flag equator_line off

########## Back to tonight in Los Alamos ####################

# date local 2016-03-04T20:30:00
# timerate rate 1
# moveto latitude 35.55 longitude -106.27 duration 10
# zoom auto initial duration 8
# wait duration 8

########## Orion nebula ###############################
select constellation Ori
flag constellation_drawing on
script action pause

flag constellation_drawing off
select nebula M42/M43

# Rawastrodata on Wikimedia Commons
# https://commons.wikimedia.org/wiki/File:The_Orion_Nebula_M42.jpg
image action load name m42 filename "images//The_Orion_Nebula_M42.jpg" coordinate_system dome alpha 1 scale 50 altitude 25 azimuth 225
script action pause

# Rogelio Bernal Andreo on Wikimedia Commons
# https://en.wikipedia.org/wiki/Orion's_Belt#/media/File:Orion_Head_to_Toe.jpg
image action load name orion filename "images//Orion_Head_to_Toe.jpg" coordinate_system dome alpha 1 scale 75 altitude 48 azimuth 140 rotation 30
script action pause

image name orion action drop
image name m42 action drop

# Don't forget Sirius
select constellation Ori
select constellation CMa
flag constellation_drawing on
select hp 32349
flag star_names on
script action pause

flag star_names off
flag constellation_drawing off
deselect

########## Spring galaxies ###############################
# Show how galaxies are clustered away from the Milky Way

# Make the Milky Way especially obvious:
set milky_way_intensity 4

timerate rate 1000
wait duration 6

timerate rate 1
flag nebulae on
flag nebula_names on
set max_mag_nebula_name 11
flag constellation_drawing on
flag constellation_names on
select constellation Vir
script action pause

flag nebula_names off
set max_mag_nebula_name 9

# How to find Virgo:
select constellation UMa
select constellation Boo
select hp 32349
flag star_names on
script action pause
flag star_names off

# Virgo galaxies:

flag constellation_drawing off
flag constellation_names off
deselect

# Show the Virgo cluster
# ESO
# https://en.wikipedia.org/wiki/File:ESO-M87.jpg
image action load name virgo filename "images//ESO-M87.jpg" coordinate_system dome alpha 1 scale 50 altitude 35 azimuth 110
script action pause

# Joseph D. Schulman on Wikimedia Commons:
# https://en.wikipedia.org/wiki/Messier_91#/media/File:Messier_91_%28M91%29.jpg
image action load name M91 filename "images/Messier_91.jpg" coordinate_system dome alpha 1 scale 50 altitude 40 azimuth 240

# Adam Block/Mount Lemmon SkyCenter/University of Arizona
# https://commons.wikimedia.org/wiki/File:M99s.jpg
image action load name M99 filename "images/M99s.jpg" coordinate_system dome alpha 1 scale 50 altitude 35 azimuth 85

# ESO by way of Wikimedia Commons
# https://en.wikipedia.org/wiki/Messier_100#/media/File:Messier_100_%E2%80%94_Grand_Design_Splendour.jpg
image action load name M100 filename "images/Messier_100_Grand_Design_Splendour.jpg" coordinate_system dome alpha 1 scale 50 altitude 60 azimuth 300

# Astrofotograf√≠a via Wikimedia Commons
# https://commons.wikimedia.org/wiki/File:M-98.jpg
image action load name M98 filename "images/M-98.jpg" coordinate_system dome alpha 1 scale 50 altitude 50 azimuth 50 rotation 65

# HST via Wikimedia Commons
# https://upload.wikimedia.org/wikipedia/commons/0/07/Messier_87_Hubble_WikiSky.jpg
image action load name M87 filename "images/Messier_87_Hubble_WikiSky.jpg" coordinate_system dome alpha 1 scale 50 altitude 30 azimuth 170

# Ken Crawford via Wikimedia Commons
# https://commons.wikimedia.org/wiki/File:Needle_Galaxy_4565.jpeg
image action load name N4565 filename "images/Needle_Galaxy_4565.jpg" coordinate_system dome alpha 1 scale 50 altitude 70 azimuth 170

script action pause
image name virgo action drop
image name M87 action drop
image name M91 action drop
image name M100 action drop
image name M99 action drop
image name M98 action drop
image name N4565 action drop

select constellation Leo
flag constellation_drawing on
script action pause

flag constellation_drawing off
select nebula M65
wait duration 4

# Hewholooks on Wikimedia Commons
# https://commons.wikimedia.org/wiki/File:LeoTripletHunterWilson.jpg
image action load name leo_trio filename "images/LeoTripletHunterWilson.jpg" coordinate_system dome alpha 1 scale 50 altitude 40 azimuth 170
script action pause

image name leo_trio action drop
deselect

select constellation Uma
flag constellation_drawing on
script action pause
flag constellation_drawing off

select nebula M81
wait duration 4
image action load name m81-m82 filename "images/m81m82_zmaritsch.jpg" coordinate_system dome alpha 1 scale 50 altitude 60 azimuth 180
script action pause

image name m81-m82 action drop
deselect
set milky_way_intensity 2

########## Other deep-sky objects ###############################

# Pleiades
select nebula "M45"
# NASA, ESA, AURA/Caltech, Palomar Observatory via Wikimedia Commons
########## Other deep-sky objects ###############################

# Pleiades
select nebula "M45"
# NASA, ESA, AURA/Caltech, Palomar Observatory via Wikimedia Commons
image action load name doublecl filename images/Pleiades_large.jpg alpha 1 coordinate_system dome scale 35 altitude 50 azimuth 180

script action pause
# Talk about: What's the nearest cluster? Pleiades? no. Hyades? no. Big Dipper!
deselect
image name pleiades action drop

# The Double Cluster from Rawastrodata via Wikimedia Commons:
# https://commons.wikimedia.org/wiki/Category:Double_Cluster#/media/File:The_Double_Cluster.jpg

select nebula "Chi & h Per"
image action load name doublecl filename images/The_Double_Cluster.jpg alpha 1 coordinate_system dome scale 35 altitude 60 azimuth 180
script action pause

deselect
image name doublecl action drop

########## Jupiter ###############################
# Show Jupiter's moons and a Galilean moon/shadow transit.
set zoom_offset -0.3

flag cardinal_points off
flag planet_names on
select planet Jupiter
wait duration 4
select planet Jupiter pointer off
flag track_object on
wait duration 5

# Zoom in to show the dance of Jupiter's moons
flag planet_names off
zoom fov .10 duration 10
wait duration 10
# This isn't actually a wait -- it just gets us to the end
# of the 5-second zoom we just did.
flag planet_names on
date local 2016-03-21T09:30:00
timerate rate 2500

wait duration 12
flag planet_names off
script action pause
# Point out the GRS

wait duration 8
# Point out Io and Europa's shadows

script action pause

date local 2016-03-04T20:00:00
timerate rate 1

flag track_object off
zoom auto initial duration 10
wait duration 10

# Turn the pointer back on since we're returning control to the operator
# (maybe not necessary?)
select pointer on
flag cardinal_points on

########### Morning planets ###########################

flag planet_names on
timerate rate 1700   # half hour per second
wait duration 5

flag ecliptic_line on
wait duration 5

select constellation Leo
select constellation Vir
select constellation Lib
select constellation Sco
select constellation Sgr
flag constellation_drawing on
flag constellation_names on
wait duration 5

timerate rate 400
wait duration 8

# Now turn the atmosphere on, so the dome will gradually lighten
flag atmosphere on
wait duration 20

flag constellation_drawing off
flag constellation_names off
flag ecliptic_line off
flag planet_names off
flag star_names off
wait duration 15

timerate rate 1
script action pause

# Can leave the script here for questions, with the lights on --
# but hitting Continue will go back to the current time, at night

# set back to the time the show will be ending
date local 2016-03-04T20:00:00
flag atmosphere off
